buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // ------ Jetty/Jersey ---------
    final jettyVersion = '9.4.8.v20171121'
    final jerseyVersion = '2.25.1'

    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'

    compile "org.eclipse.jetty:jetty-server:$jettyVersion"
    compile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    compile "org.eclipse.jetty:jetty-servlets:$jettyVersion"

    compile("org.glassfish.jersey.core:jersey-server:$jerseyVersion") {
        exclude group: 'org.glassfish.jersey.bundles.repackaged'
    }
    compile "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    compile "org.glassfish.jersey.media:jersey-media-multipart:$jerseyVersion"
    compile 'org.glassfish.hk2:guice-bridge:2.5.0-b36'

    // ------ Jackson ------
    final jacksonVersion = '2.8.2'

    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jacksonVersion"
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.+'

    // ------- Logging ------
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'

    // ------- Logging ------
    // https://mvnrepository.com/artifact/com.google.inject.extensions/guice-multibindings
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '3.0'

    // ------ MySQL Java -----
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.13'
}

jar {
    baseName = "simple-world"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest { attributes 'Main-Class': 'com.simple.server.Application' }
}

task copyJar(type: Copy) {
    dependsOn 'jar'
    from "build/install/simple"
    into 'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

task buildImage(type: DockerBuildImage) {
    dependsOn copyJar
    if (System.env.DOCKER_HOST) {
        url = "$System.env.DOCKER_HOST".replace("tcp", "https")
        if (System.env.DOCKER_CERT_PATH) {
            certPath = new File(System.env.DOCKER_CERT_PATH)
        }
    } else {
        url = 'unix:///var/run/docker.sock'
    }
    inputDir = file('.')
    tag = 'simple-server'
}

//build.dependsOn buildImage
//build.dependsOn copyJar


allprojects {

    ext {
        mainAppClassName = 'com.simple.server.Application'
    }
    mainClassName = mainAppClassName
}
